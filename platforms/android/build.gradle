
println "#####################################"
println "### Running with Gradle: $gradle.gradleVersion"
println "#####################################"

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.google.gms:google-services:4.3.3'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.application'

//
//  Add Firebase
//
project.ext {
  // Configure the Firebase C++ SDK location.
  firebase_cpp_sdk_dir = System.getProperty('firebase_cpp_sdk.dir')
  printf('Firebase C++ SDK directory %s', firebase_cpp_sdk_dir)
  if (!(new File(firebase_cpp_sdk_dir)).exists()) {
    firebase_cpp_sdk_dir = "../../virgil-messenger-qt/" +  System.getProperty('firebase_cpp_sdk.dir')
    if (!(new File(firebase_cpp_sdk_dir)).exists()) {
        throw new StopActionException(
          sprintf('Firebase C++ SDK directory %s does not exist',
              firebase_cpp_sdk_dir))
    }
  }
}


repositories {
    flatDir {
        dirs project.ext.firebase_cpp_sdk_dir + "/libs/android"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    compile 'com.google.firebase:firebase-core:15.0.2'
    compile 'com.google.firebase:firebase-auth:19.3.1'
    compile 'com.google.firebase:firebase-messaging:20.1.7'
    compile 'com.google.firebase.messaging.cpp:firebase_messaging_cpp@aar'
    compile 'com.google.android.gms:play-services-base:17.2.1'
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    compileSdkVersion androidCompileSdkVersion.toInteger()
    buildToolsVersion androidBuildToolsVersion.toString()

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 25
    }
}

apply plugin: 'com.google.gms.google-services'
